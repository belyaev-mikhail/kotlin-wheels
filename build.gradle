buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "1.4.10"
    id 'maven-publish'
}

def overwriteVersion = project.findProperty("forceVersion") ?: '0.0.1.0'

group 'ru.spbstu'
version overwriteVersion

repositories {
    maven { url 'https://dl.bintray.com/vorpal-research/kotlin-maven' }
    jcenter()
}

kotlin {
    jvm()
    js {
        nodejs()
        browser()
    }

    sourceSets {
        all {
            languageSettings {
                languageVersion = '1.4'
                apiVersion = '1.4'
                enableLanguageFeature 'InlineClasses'
                useExperimentalAnnotation 'kotlin.RequiresOptIn'
                useExperimentalAnnotation 'kotlin.ExperimentalUnsignedTypes'
                useExperimentalAnnotation 'kotlin.ExperimentalStdlibApi'
            }
        }

        commonMain {
            dependencies {
                implementation 'ru.spbstu:kotlinx-warnings:1.4.10'
            }
        }
        commonTest {
            dependsOn(commonMain)
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependsOn(commonMain)
        }
        jvmTest {
            dependsOn(commonTest)
            dependsOn(jvmMain)
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-testng')
            }
        }
        jsMain {
            dependsOn(commonMain)
        }
        jsTest {
            dependsOn(commonTest)
            dependsOn(jsMain)
            dependencies {
                api kotlin('test-js')
            }
        }
    }

}

publishing {
    repositories {
        maven {
            url = "https://api.bintray.com/maven/${bintrayOrg}/${bintrayRepo}/${project.name}/;publish=1;override=1"
            credentials {
                username (project.findProperty('bintrayUsername') ?: System.getenv('BINTRAY_USERNAME'))
                password (project.findProperty('bintrayPassword') ?: System.getenv('BINTRAY_PASSWORD'))
            }
        }
    }
}
